import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.3'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'org.hibernate.orm' version '6.4.4.Final'
	id 'org.graalvm.buildtools.native' version '0.9.28'
	id 'org.jetbrains.kotlin.jvm' version '1.9.22'
	id 'org.jetbrains.kotlin.plugin.spring' version '1.9.22'
	id 'org.jetbrains.kotlin.plugin.jpa' version '1.9.22'
	id 'com.github.gmazzo.buildconfig' version '5.3.5'
}

group = 'com.krillsson'
version = '0.0.1-SNAPSHOT'

buildConfig {
	buildConfigField("String", "APP_VERSION", provider { "\"${project.version}\"" })
	buildConfigField("java.util.Date", "BUILD_TIME", "java.util.Date(" + System.currentTimeMillis() + "L)")
}

java {
	sourceCompatibility = '21'
}

repositories {
	mavenCentral()
	flatDir {
		dirs 'lib'
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	// implementation 'org.springframework.boot:spring-boot-starter-graphql'
	implementation 'org.springframework.boot:spring-boot-starter-quartz'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	implementation 'org.xerial:sqlite-jdbc:3.44.1.0'
	implementation 'org.hibernate.orm:hibernate-community-dialects'
	implementation 'com.github.docker-java:docker-java-core:3.3.4'
	implementation 'com.github.docker-java:docker-java-transport-httpclient5:3.3.4'
	implementation 'com.squareup.retrofit2:retrofit:2.9.0'
	implementation 'com.squareup.retrofit2:converter-scalars:2.9.0'
	implementation 'com.squareup.retrofit2:converter-jackson:2.9.0'
	implementation 'org.bitlet:weupnp:0.1.4'
	implementation 'org.jmdns:jmdns:3.5.1'
	implementation 'org.apache.commons:commons-exec:1.3'
	compileOnly 'jakarta.servlet:jakarta.servlet-api:6.0.0'
	implementation('com.graphql-java-kickstart:graphql-java-tools:13.1.1') {
		exclude group: "org.slf4j"
	}
	implementation('com.graphql-java-kickstart:graphql-java-servlet:15.2.0') {
		exclude group: "org.slf4j"
	}
	implementation('com.github.oshi:oshi-core:6.4.12') {
		exclude group: "org.slf4j"
	}
	implementation name: 'jni4net.j-0.8.9.0'
	implementation name: 'OhmJniWrapper.j4n'
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
	implementation "org.springframework.boot:spring-boot-configuration-processor"
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework:spring-webflux'
	testImplementation 'org.springframework.graphql:spring-graphql-test'
	testImplementation 'org.springframework.security:spring-security-test'
}

tasks.withType(KotlinCompile) {
	kotlinOptions {
		freeCompilerArgs += '-Xjsr305=strict'
		jvmTarget = '21'
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

hibernate {
	enhancement {
		enableAssociationManagement = true
	}
}

